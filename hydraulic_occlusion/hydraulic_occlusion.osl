/*
 * This file is part of Jakub Jaszewski' OSL repository.
 * https://github.com/j-jasz/OSL
 * Copyright (C) 2025 Jakub Jaszewski.
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program. If not, see <https://www.gnu.org/licenses/>.
 */

void unit_sphere (vector N,
                            vector Normal,
                            float Ray_Angle,
                            float Ray_Spread,
                            float Ray_Tilt,
                            float rand,
                            output float x_dir,
                            output float y_dir,
                            output float z_dir
){
    vector Dir = N - dot(N, Normal) * Normal;
    Dir = normalize(Dir);
    float ray_tilt = Ray_Tilt*-1 - 1;
    float max_angle = Ray_Angle * M_PI / 2;
    float max_spread = Ray_Spread * M_PI / 2;
    float fin_angle = rand * max_angle * 2 - max_angle;
    float fin_spread = rand * max_spread * 2 - max_spread;
    float theta = atan2(Dir[1], Dir[0]) + fin_angle;
    float phi = acos(Dir[2]) * fin_spread;
    x_dir = cos(theta);
    y_dir = sin(theta);
    z_dir = cos(phi) + ray_tilt;
}

shader hydraulic_occlusion(
    int Samples = 4,
    float Max_Distance = 1.0,
    float Min_Distance = 0.001,
    float Ray_Angle = 1.0,
    float Ray_Spread = 0.0,
    float Ray_Tilt = 0.0,
    float Hit_Void_Thold = 10.0,
    vector Normal = vector(0, 0, 1),
    output color Hydraulic_Occlusion = 0,
    output color Distance_per_Sample = 0.0,
    output color Hit_Void = 0.0
) {
    // declare variables here, to save registers by reusing them in the Samples loop
    int i;
    int hit_count = 0;
    float hit_accum = 0.0;
    float hitdist = 0.0;
    float mix_ho = 0.0;
    float mix_dist = 0.0;
    float mix_void = 0.0;
    float seed = fmod((hashnoise(P, 123456.0)), 1.0);
    float rand, x_dir, y_dir, z_dir;
    vector ray_dir, hit_nor;

    // ignore shading points on the backface
    if (!backfacing()) {
        for (i = 0; i < Samples; i++) {
            rand = hashnoise(P, seed + i * 0.1);
            unit_sphere(N, Normal, Ray_Angle, Ray_Spread, Ray_Tilt, rand, x_dir, y_dir, z_dir);
            ray_dir = vector(x_dir, y_dir, z_dir);
            // t is always 1 inside the If block, we don't need it.
            if (trace(P, ray_dir, "maxdist", Max_Distance)){
                getmessage("trace", "hitdist", hitdist);
                getmessage("trace", "N", hit_nor);
                // check if the face is hit on the front or back and ignore rays shorter than
                if (dot(hit_nor, ray_dir) < 0 && hitdist >= Min_Distance) {
                    // hitdist is always positive, no need to clamp at 0
                    hit_accum += hitdist;
                    hit_count += 1;
                    mix_ho += min(1/hit_accum, 1.0);
                }
            }
        }
    }
    mix_ho /= Samples;
    mix_dist = hit_accum / Samples;
    mix_void = hit_count > Hit_Void_Thold;
    color ColorHit = color(0.0, 0.0, 0.0);
    color ColorIn = color(1.0, 1.0, 1.0);
    Hydraulic_Occlusion = mix(ColorHit, ColorIn, mix_ho);
    Distance_per_Sample = mix(ColorHit, ColorIn, mix_dist);
    Hit_Void = mix(ColorIn, ColorHit, mix_void);
}





